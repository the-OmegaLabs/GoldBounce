plugins {
    id "java"
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.8.0"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "2.3-SNAPSHOT"
    id "org.spongepowered.mixin"
    id "com.gorylenko.gradle-git-properties" version "2.4.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url = "https://maven.minecraftforge.net/" }
    maven { url = "https://maven.aliyun.com/repository/public/"}
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://repo.viaversion.com/everything"}
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += ["-Dfml.coreMods.load=net.ccbluex.liquidbounce.injection.forge.MixinLoader", "-Xmx4096m", "-Xms1024m", "-Ddev-mode"]
}

configurations {
    include
    implementation.extendsFrom(include)

    runtimeOnly.canBeResolved = true
}

dependencies {
    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
        exclude module: "slf4j-api"
    }
    implementation("javax.vecmath:vecmath:1.5.2")

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    include "com.jagrosh:DiscordIPC:0.4"

    include("com.github.CCBlueX:Elixir:1.2.6") {
        exclude module: "kotlin-stdlib"
        exclude module: "authlib"
    }
    include "org.apache.httpcomponents:httpmime:4.5.14"
//    implementation("io.ktor:ktor-client-core:2.3.10")
//    implementation("io.ktor:ktor-client-cio:2.3.10")
//    implementation("io.ktor:ktor-client-websockets:2.3.10")
//    implementation("io.ktor:ktor-client-content-negotiation:2.3.10")
//    implementation("io.ktor:ktor-serialization-kotlinx-json:2.3.10")
//    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
    include "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3"
    implementation 'javazoom:jlayer:1.0.1'
    include fileTree(include: ["*.jar"], dir: "libs")
    include("org.knowm.xchart:xchart:3.8.8")
    include "org.projectlombok:lombok:1.18.24"
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "LICENSE.txt"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"

    exclude "org/apache/log4j/**"
    exclude "org/apache/commons/**"
    exclude "org/junit/**"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename "(.+_at.cfg)", "META-INF/\$1"
}

tasks.register('moveResources') {
    doLast {
        copy {
            from "${buildDir}/resources/main"
            into "${buildDir}/classes/java"
        }
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)

jar {
    manifest.attributes(
            "FMLCorePlugin": "net.ccbluex.liquidbounce.injection.forge.MixinLoader",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "liquidbounce.forge.mixins.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "liquidbounce_at.cfg"
    )

    enabled = false
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "liquidbounce.mixins.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

jar.dependsOn("shadowJar")

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.generateGitProperties.enabled = false

tasks.register('copyZipInclude', Copy) {
    from 'zip_include/'
    into 'build/libs/zip'
}

build.dependsOn copyZipInclude